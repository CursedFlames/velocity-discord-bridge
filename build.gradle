plugins {
	id "com.github.johnrengelman.shadow" version "8.1.1"
	id "java"
}

repositories {
	maven {
		name = "papermc"
		url = uri("https://repo.papermc.io/repository/maven-public/")
	}
	mavenCentral()
}

java {
	toolchain {
		languageVersion.set(JavaLanguageVersion.of(17))
	}
}

configurations {
	shaded
	implementation.extendsFrom(shaded)
}

dependencies {
	compileOnly("com.velocitypowered:velocity-api:${velocity_api_version}")
	annotationProcessor("com.velocitypowered:velocity-api:${velocity_api_version}")

	shaded("club.minnced:discord-webhooks:${discord_webhook_version}")
	shaded("net.dv8tion:JDA:${jda_version}") {
		// We don't need audio features
		exclude module: 'opus-java'
	}
	shaded("dev.vankka:mcdiscordreserializer:${mcdiscord_reserializer_version}")
}

jar {
	archiveClassifier.set("thin")
}

// TODO should relocate shaded jars for compatibility
shadowJar {
	archiveClassifier.set("")

	configurations = [project.configurations.shaded]
}

build.dependsOn shadowJar

